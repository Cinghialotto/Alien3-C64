
****************************************
*	Whole redraw with colour	*
****************************************
ALLSCREEN	LDA	#8		; tiles down screen
	STA	YTEMP		; count save
	LDA	#>SCREEN1
	STA	SCREEN
	STA	COLOUR
	LDA	#<SCREEN1
	STA	SCREEN+1
	LDA	#<NYBBLE
 	STA	COLOUR+1

	LDX	YOFFSET		; yoffset down map for start
	STX	XTEMP
NEXTLINE	LDA	XOFFSET		; x offset into map (should be 0)
	CLC
	ADC	GMAP_L,X
	STA	MAP_MEM+1
	LDA	GMAP_H,X
	ADC	#0
	STA	MAP_MEM+2
	LDA	#20		; x loop
	STA	TEMP
	LDA	#0
	STA	MIKE1		; map offset
ACROSS	LDY	MIKE1		; map offset
MAP_MEM	LDX	&FFFF,Y		; tile byte for map
	LDY	#0		; now get screen offset
	LDA	TILE_TL,X
	STA	(SCREEN),Y
	LDA	TCOL_TL,X
	STA	(COLOUR),Y
	INY
	LDA	TILE_TR,X
	STA	(SCREEN),Y
	LDA	TCOL_TR,X
	STA	(COLOUR),Y
	LDY	#40		; next line
	LDA	TILE_BL,X
	STA	(SCREEN),Y
	LDA	TCOL_BL,X
	STA	(COLOUR),Y
	INY
	LDA	TILE_BR,X
	STA	(SCREEN),Y
	LDA	TCOL_BR,X
	STA	(COLOUR),Y
	INC	MIKE1		; map offset next tile x
	LDA	SCREEN
	CLC
	ADC	#2		; 2 bytes along
	STA	SCREEN
	STA	COLOUR
	BCC	MOST_TIME	
	INC	SCREEN+1
	INC	COLOUR+1	
MOST_TIME	DEC	TEMP		; along screen
	BNE	ACROSS
	LDA	SCREEN		; 2 lines down for next tile place
	CLC
	ADC	#40
	STA	SCREEN
	STA	COLOUR
	BCC	GOLD
	INC	SCREEN+1
	INC	COLOUR+1
GOLD	INC	XTEMP		; next line in map
	LDX	XTEMP
	DEC	YTEMP		; y loop
	BNE	NEXTLINE
	RTS

****************************************
*	MAP Redraws without colour	*
****************************************

	;  TINX=0 TINY=0

RE_PLOT	LDA	TINY
	BEQ	SUMMER
	LDA	TINX
	BNE	WINTER
	JMP	RE_PLOT3
WINTER	JMP	RE_PLOT4
		
SUMMER	LDA	TINX
	BEQ	RE_PLOT1
	JMP	RE_PLOT2

TILE1	MACRO
	LDY	#@1
	LDA	(MIKE2),Y
	TAX
	LDA	TILE_TL,X
	IF	@1<>@2
	LDY	#@2
	ENDIF
	STA	(SCREEN),Y
	INY
	LDA	TILE_TR,X
	STA	(SCREEN),Y
	LDY	#@2+40		; next line
	LDA	TILE_BL,X
	STA	(SCREEN),Y
	INY
	LDA	TILE_BR,X
	STA	(SCREEN),Y
	ENDM

TILE2	MACRO
	LDY	#@1
	LDA	(MIKE2),Y
	TAX
	IF	@1<>@2
	LDY	#@2
	ENDIF
	LDA	TILE_BL,X
	STA	(SCREEN),Y
	INY
	LDA	TILE_BR,X
	STA	(SCREEN),Y
	ENDM

TILE3	MACRO
	LDY	#@1		; now get screen offset
	LDA	(MIKE2),Y
	TAX
	IF	@1<>@2
	LDY	#@2
	ENDIF
	LDA	TILE_TL,X
	STA	(SCREEN),Y
	INY
	LDA	TILE_TR,X
	STA	(SCREEN),Y
	ENDM
	
RE_PLOT1	LDA	#>SCREEN1
	STA	SCREEN
	LDA	#<SCREEN1
	STA	SCREEN+1
	LDX	YOFFSET		; yoffset down map for start
	STX	XTEMP
	LDA	#8
	STA	YTEMP
NEXTLINE1	LDA	XOFFSET		; x offset into map (should be 0)
	CLC
	ADC	GMAP_L,X
	STA	MIKE2+0
	LDA	GMAP_H,X
	ADC	#0
	STA	MIKE2+1
	TILE1	0,0
	TILE1	1,2
	TILE1	2,4
	TILE1	3,6
	TILE1	4,8
	TILE1	5,10
	TILE1	6,12
	TILE1	7,14
	TILE1	8,16
	TILE1	9,18
	TILE1	10,20
	TILE1	11,22
	TILE1	12,24
	TILE1	13,26
	TILE1	14,28
	TILE1	15,30
	TILE1	16,32
	TILE1	17,34
	TILE1	18,36
	TILE1	19,38
	LDA	SCREEN		; 2 lines down for next tile place
	CLC
	ADC	#80
	STA	SCREEN
	BCC	!HIGH2
	INC	SCREEN+1
!HIGH2	INC	XTEMP		; next line in map
	LDX	XTEMP
	DEC	YTEMP		; y loop
	BEQ	ENDP1
	JMP	NEXTLINE1
ENDP1	RTS

	; TINX = 8 TINY = 0
RE_PLOT2	LDA	#>SCREEN1
	STA	SCREEN
	LDA	#<SCREEN1
	STA	SCREEN+1
	LDX	YOFFSET		; yoffset down map for start
	STX	XTEMP
	LDA	#8
	STA	YTEMP
NEXTLINE2	LDA	XOFFSET		; x offset into map (should be 0)
	CLC
	ADC	GMAP_L,X
	STA	MIKE2+0
	LDA	GMAP_H,X
	ADC	#0
	STA	MIKE2+1

	LDY	#0
	LDA	(MIKE2),Y
	TAX
	LDA	TILE_TR,X		; top right
	STA	(SCREEN),Y
	LDY	#40
	LDA	TILE_BR,X		; bottom right
	STA	(SCREEN),Y

	TILE1	1,1
	TILE1	2,3
	TILE1	3,5
	TILE1	4,7
	TILE1	5,9
	TILE1	6,11
	TILE1	7,13
	TILE1	8,15
	TILE1	9,17
	TILE1	10,19
	TILE1	11,21
	TILE1	12,23
	TILE1	13,25
	TILE1	14,27
	TILE1	15,29
	TILE1	16,31
	TILE1	17,33
	TILE1	18,35
	TILE1	19,37
	LDY	#20
	LDA	(MIKE2),Y
	TAX
	LDY	#39
	LDA	TILE_TL,X
	STA	(SCREEN),Y
	LDY	#39+40
	LDA	TILE_BL,X
	STA	(SCREEN),Y
	LDA	SCREEN		; 2 lines down for next tile place
	CLC
	ADC	#80
	STA	SCREEN
	BCC	!HIGH2
	INC	SCREEN+1
!HIGH2	INC	XTEMP		; next line in map
	LDX	XTEMP
	DEC	YTEMP		; y loop
	BEQ	ENDP2
	JMP	NEXTLINE2
ENDP2	RTS

	; TINX=0 TINY=8
RE_PLOT3
	LDA	#>SCREEN1
	STA	SCREEN
	LDA	#<SCREEN1
	STA	SCREEN+1
	LDX	YOFFSET		; yoffset down map for start
	STX	XTEMP
	LDA	XOFFSET
	CLC
	ADC	GMAP_L,X
	STA	MIKE2+0
	LDA	GMAP_H,X
	ADC	#0
	STA	MIKE2+1
	TILE2	0,0
	TILE2	1,2
	TILE2	2,4
	TILE2	3,6
	TILE2	4,8
	TILE2	5,10
	TILE2	6,12
	TILE2	7,14
	TILE2	8,16
	TILE2	9,18
	TILE2	10,20
	TILE2	11,22
	TILE2	12,24
	TILE2	13,26
	TILE2	14,28
	TILE2	15,30
	TILE2	16,32
	TILE2	17,34
	TILE2	18,36
	TILE2	19,38
	LDA	#40
	STA	SCREEN
	LDA	#7
	STA	YTEMP
	LDX	XTEMP
NEXTLINE3	LDA	XOFFSET		; x offset into map (should be 0)
	CLC
	ADC	GMAP_L+1,X
	STA	MIKE2+0
	LDA	GMAP_H+1,X
	ADC	#0
	STA	MIKE2+1
	TILE1	0,0
	TILE1	1,2
	TILE1	2,4
	TILE1	3,6
	TILE1	4,8
	TILE1	5,10
	TILE1	6,12
	TILE1	7,14
	TILE1	8,16
	TILE1	9,18
	TILE1	10,20
	TILE1	11,22
	TILE1	12,24
	TILE1	13,26
	TILE1	14,28
	TILE1	15,30
	TILE1	16,32
	TILE1	17,34
	TILE1	18,36
	TILE1	19,38
	LDA	SCREEN		; 2 lines down for next tile place
	CLC
	ADC	#80
	STA	SCREEN
	BCC	!HIGH2
	INC	SCREEN+1
!HIGH2	INC	XTEMP		; next line in map
	LDX	XTEMP
	DEC	YTEMP		; y loop
	BEQ	ENDP3
	JMP	NEXTLINE3
ENDP3	LDA	XOFFSET
	CLC
	ADC	GMAP_L+1,X
	STA	MIKE2+0
	LDA	GMAP_H+1,X
	ADC	#0
	STA	MIKE2+1
	TILE3	0,0
	TILE3	1,2
	TILE3	2,4
	TILE3	3,6
	TILE3	4,8
	TILE3	5,10
	TILE3	6,12
	TILE3	7,14
	TILE3	8,16
	TILE3	9,18
	TILE3	10,20
	TILE3	11,22
	TILE3	12,24
	TILE3	13,26
	TILE3	14,28
	TILE3	15,30
	TILE3	16,32
	TILE3	17,34
	TILE3	18,36
	TILE3	19,38
	RTS

	; TINX = 8 TINY = 8
RE_PLOT4	LDA	#>SCREEN1
	STA	SCREEN
	LDA	#<SCREEN1
	STA	SCREEN+1
	LDX	YOFFSET		; yoffset down map for start
	STX	XTEMP
	LDA	XOFFSET
	CLC
	ADC	GMAP_L,X
	STA	MIKE2+0
	LDA	GMAP_H,X
	ADC	#0
	STA	MIKE2+1
	LDY	#0
	LDA	(MIKE2),Y		; get first block
	TAX
	LDA	TILE_BR,X		; bottom right bit
	STA	(SCREEN),Y
	TILE2	1,1
	TILE2	2,3
	TILE2	3,5
	TILE2	4,7
	TILE2	5,9
	TILE2	6,11
	TILE2	7,13
	TILE2	8,15
	TILE2	9,17
	TILE2	10,19
	TILE2	11,21
	TILE2	12,23
	TILE2	13,25
	TILE2	14,27
	TILE2	15,29
	TILE2	16,31
	TILE2	17,33
	TILE2	18,35
	TILE2	19,37
	LDY	#20
	LDA	(MIKE2),Y		; get first block
	TAX
	LDA	TILE_BL,X		; bottom right bit
	LDY	#39
	STA	(SCREEN),Y

	LDA	#40
	STA	SCREEN
	LDA	#7		; tiles down screen
	STA	YTEMP		; count save
	LDX	XTEMP
NEXTLINE4	LDA	XOFFSET		; x offset into map (should be 0)
	CLC
	ADC	GMAP_L+1,X
	STA	MIKE2+0
	LDA	GMAP_H+1,X
	ADC	#0
	STA	MIKE2+1

	LDY	#0
	LDA	(MIKE2),Y
	TAX
	LDA	TILE_TR,X		; top right
	STA	(SCREEN),Y
	LDY	#40
	LDA	TILE_BR,X		; bottom right
	STA	(SCREEN),Y
	TILE1	1,1
	TILE1	2,3
	TILE1	3,5
	TILE1	4,7
	TILE1	5,9
	TILE1	6,11
	TILE1	7,13
	TILE1	8,15
	TILE1	9,17
	TILE1	10,19
	TILE1	11,21
	TILE1	12,23
	TILE1	13,25
	TILE1	14,27
	TILE1	15,29
	TILE1	16,31
	TILE1	17,33
	TILE1	18,35
	TILE1	19,37
	LDY	#20
	LDA	(MIKE2),Y
	TAX
	LDY	#39
	LDA	TILE_TL,X
	STA	(SCREEN),Y
	LDY	#39+40
	LDA	TILE_BL,X
	STA	(SCREEN),Y

	LDA	SCREEN		; 2 lines down for next tile place
	CLC
	ADC	#80
	STA	SCREEN
	BCC	!HIGH2
	INC	SCREEN+1
!HIGH2	INC	XTEMP		; next line in map
	LDX	XTEMP
	DEC	YTEMP		; y loop
	BEQ	ENDP4
	JMP	NEXTLINE4

ENDP4	LDA	XOFFSET
	CLC
	ADC	GMAP_L+1,X
	STA	MIKE2+0
	LDA	GMAP_H+1,X
	ADC	#0
	STA	MIKE2+1
	LDY	#0
	LDA	(MIKE2),Y		; get first block
	TAX
	LDA	TILE_TR,X		; bottom right bit
	STA	(SCREEN),Y
	TILE3	1,1
	TILE3	2,3
	TILE3	3,5
	TILE3	4,7
	TILE3	5,9
	TILE3	6,11
	TILE3	7,13
	TILE3	8,15
	TILE3	9,17
	TILE3	10,19
	TILE3	11,21
	TILE3	12,23
	TILE3	13,25
	TILE3	14,27
	TILE3	15,29
	TILE3	16,31
	TILE3	17,33
	TILE3	18,35
	TILE3	19,37
	LDY	#20
	LDA	(MIKE2),Y
	TAX
	LDA	TILE_TL,X
	LDY	#39
	STA	(SCREEN),Y
	RTS

