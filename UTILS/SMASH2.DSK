 
	SEND COMPUTER2


;Smash TV - Part 2 Main Game Loader

	IF	>$FF00
	ERROR	"Your <> are the wrong way round!"
	ENDIF


LOADER	EQU	$400	;address to put loader to

	ORG	LOADER
!NEXT	LDX	#0
	LDA	FILES1,X
	BNE	!NOT_FF
	JMP	$200	;& run it!	
!NOT_FF	STA	LOADADD+2
	LDA	FILES1+1,X
	STA	FNAME
	LDA	FILES1+2,X
	STA	FNAME+1
	INX
	INX
	INX
	STX	!NEXT+1
!GOT_NAM	JSR	DLOAD
	BCS	!NEXT
!ERROR	INC	$D020
	JMP	!ERROR

MY_BYTE
FILES1	DB	$2
FNAME	DB	"M1"
	DB	$B0
	DB	"M2"
	DB	$C0
	DB	"M3"
	DB	$00

;load a file from disk
;(LOADADD)->points to load address
;FNAME,FNAME+1 contains a two byte file name
;Returns with CC on error, else CS
DLOAD	LDA	#$24
	STA	$DD00
	JSR	INIT_TALK
	JSR	SEND_BYTE
	LDA	FNAME
	JSR	SEND_BYTE
	LDA	FNAME+1
	JSR	SEND_BYTE
	JSR	INIT_TALK
	JSR	GET_BYTE ;skip load address from file
	JSR	GET_BYTE
	LDY	#$0
LOOPX	JSR	GET_BYTE
	CMP	#$AC
	BNE	XSTORE_IT
	JSR	GET_BYTE
	CMP	#$AC
	BEQ	XSTORE_IT ;really was a $ac
	CMP	#$FF
	BEQ	ALL_DONE
	CMP	#$F7
	BEQ	ERROR
	JSR	INIT_TALK ;oops
	BEQ	LOOPX
XSTORE_IT	INC	$D020
	DEC	$1
LOADADD	STA	$8000,Y
	INC	$1
	DEC	$D020
	INY	
	BNE	LOOPX
	INC	LOADADD+2
	BNE	LOOPX
ERROR	CLC	
ALL_DONE	RTS	

GET_BYTE	JSR	GET_2BITS
	JSR	GET_2BITS
	JSR	GET_2BITS
	JSR	GET_2BITS
	JSR	!RET
	LDA	MY_BYTE
!RET	RTS	
GET_2BITS	LDX	#$94
	LDA	$DD00
	STX	$DD00
	ASL	A
	ROR	MY_BYTE
	PHA	
	PLA	
	PHA	
	PLA	
L764E	LDX	#$C4
	LDA	$DD00
	STX	$DD00
	ASL	A
	ROR	MY_BYTE
	RTS	
SEND_BYTE	STA	MY_BYTE
L765C	JSR	SEND_2BITS
	JSR	SEND_2BITS
	JSR	SEND_2BITS
	JSR	SEND_2BITS
	RTS	

SEND_2BITS	LDA	#$14
	LSR	MY_BYTE
	BCC	!BIT0
	ORA	#$20
!BIT0	STA	$DD00
	NOP	
	NOP	
	NOP	
	NOP	
L7678	LDA	#$4
	LSR	MY_BYTE
	BCC	!BIT0
	ORA	#$20
!BIT0	STA	$DD00
	NOP	
	NOP	
	RTS
	
INIT_TALK	LDX	#$32
!LOOP	DEX	
	BNE	!LOOP
LA088	LDA	$DD00
	AND	#$40
	BEQ	LA088
L7692	LDA	#$C4
	STA	$DD00
	LDX	#$5
!LOOP	DEX	
	BNE	!LOOP
	RTS
ENDLOAD

	END
